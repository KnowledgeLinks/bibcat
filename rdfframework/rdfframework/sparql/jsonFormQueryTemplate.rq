prefix acl: <http://www.w3.org/ns/auth/acl#> 
prefix foaf: <http://xmlns.com/foaf/0.1/> 
prefix kds: <http://knowledgelinks.io/ns/data-structures/> 
prefix kdr: <http://knowledgelinks.io/ns/data-resources/> 
prefix obi: <https://w3id.org/openbadges#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix schema: <https://schema.org/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT *
{
	GRAPH <http://knowledgelinks.io/ns/application-framework/>
	{
		SELECT (CONCAT('{',GROUP_CONCAT(?form; SEPARATOR=','),'}') As ?appForms)
		{
			{
				SELECT ?klForms (GROUP_CONCAT(?pV; SEPARATOR=',') as ?po) (COUNT(?klForms) AS ?poTest)
				{
					{
						SELECT ?klForms ?p  (GROUP_CONCAT(?oVf; SEPARATOR=',') as ?oa) (COUNT(?p) AS ?oaTest)
						{
							{
								SELECT ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  (GROUP_CONCAT(?pV1; SEPARATOR=',') as ?p1o) (COUNT(?p) AS ?p1oTest)
								{
									{
										SELECT ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ (GROUP_CONCAT(?oV1; SEPARATOR=',') as ?o1a) (COUNT(?p1_) AS ?o1aTest)
										{
											{
												SELECT ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ (GROUP_CONCAT(?pV2; SEPARATOR=',') as ?p2o) (COUNT(?o1_) AS ?p2oTest)
												{
													{
														SELECT ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ ?p2_ (GROUP_CONCAT(?oV2; SEPARATOR=',') as ?o2a) (COUNT(?p2_) AS ?o2aTest)
														{
															{
																SELECT ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ ?p2_ ?o2_  (GROUP_CONCAT(?pV3; SEPARATOR=',') as ?p3o) (COUNT(?o2_) AS ?p3oTest)
																{
																	{
																		SELECT ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ ?p2_ ?o2_ ?p3_ (GROUP_CONCAT(?oV3; SEPARATOR=',') as ?o3a) (COUNT(?p3_) AS ?o3aTest)
																		{
																			{
																				# Union query to gather all relevant information
																				SELECT ?klForms ?p ?o ?1st ?fieldOrder ?propClass ?p1_ ?o1_ ?p2_ ?o2_ ?p3_ ?o3_
																				{
																					{
																						# Get the form related info
																						SELECT DISTINCT ?klForms ?p ?o ("_____" as ?1st) (-1 as ?fieldOrder) ("_____" AS ?propClass)  ?p1 ?o1 ?p2 ?o2 ?p3 ?o3
																						{
																							?klForms a kds:FormClass .
																							?klForms ?p ?o .
																							FILTER (!(kds:hasProperty=?p)).
																							OPTIONAL {
																								?o ?p1 ?o1 .
																								OPTIONAL {
																									?o1 ?p2 ?o2 .
																									FILTER(ISBLANK(?o1))
																								}
																								FILTER(ISBLANK(?o))
																							} .
																						}
																					} UNION {
																						# Get class related info 
																						SELECT DISTINCT ?klForms ?p ?o (?property as ?1st) ?fieldOrder ?propClass  ("classInfo" AS ?p1) ("_____" AS ?o1) ?p2 ?o2 ?p3 ?o3
																						{
																							?klForms a kds:FormClass .
																							?klForms ?p ?o .
																							BIND(?o AS ?bn ).
																							?o kds:propUri ?property .
																							?o kds:formFieldOrder ?fieldOrder .
																							FILTER(isBlank(?o)) .
																							OPTIONAL {?o kds:classUri ?classUri } .
																							?property rdfs:domain ?domain .
																							BIND(COALESCE(?classUri,?domain) AS ?propClass) .
																							?propClass ?p2 ?o2 .
																							FILTER(?p2=kds:classSecurity||?p2=kds:primaryKey) .
																							OPTIONAL {
																								?o2 ?p3 ?o3 .
																								FILTER(isBlank(?o2))
																							} .
																						}
																					} UNION {
																						SELECT DISTINCT ?klForms ?p ?o (?property as ?1st) ?fieldOrder ?propClass  ?p1 ?o1 ?p2 ?o2 ?p3 ?o3
																						{
																							?klForms a kds:FormClass .
																							?klForms ?p ?o .
																							BIND(?o AS ?bn ).
																							FILTER(isBlank(?o))
																							?o kds:propUri ?property .
																							?o kds:formFieldOrder ?fieldOrder .
																							OPTIONAL {?o kds:classUri ?classUri } .
																							?property rdfs:domain ?domain .
																							BIND(COALESCE(?classUri,?domain) AS ?propClass) .	
																							?o ?p1 ?o1 .
																							FILTER(ISBLANK(?o1))
																							FILTER(!(kds:classUri=?p1))
																							?o1 ?p2 ?o2 .
																							OPTIONAL {
																								?o2 ?p3 ?o3 .
																								FILTER(isBlank(?o2))
																							} .
																							
																						}
																					} UNION {	
																						# Get the details from the form non-blank node hasProperty properties
																						SELECT DISTINCT ?klForms ?p ?o (?property as ?1st) ?fieldOrder ?propClass  ?p1 ?o1 ?p2 ?o2 ?p3 ?o3
																						{
																							?klForms a kds:FormClass .
																							?klForms ?p ?o .
																							?o kds:propUri ?property .
																							?o kds:formFieldOrder ?fieldOrder .
																							FILTER(isBlank(?o))
																							OPTIONAL {?o kds:classUri ?classUri } .
																							?property rdfs:domain ?domain .
																							BIND(COALESCE(?classUri,?domain) AS ?propClass) .	
																							?o ?p1 ?o1 .
																							FILTER(!(ISBLANK(?o1)))
																							FILTER(!(kds:classUri=?p1))
																						}
																					} UNION {
																						# Get the property details from the property definition
																						SELECT ?klForms ?p ?o (?property as ?1st) ?fieldOrder ?propClass  ?p1 ?o1 ?p2 ?o2 ?p3 ?o3
																						{
																							{
																								#SELECT ?klForms ?p ?o ?property ?fieldOrder ?propClass
																								{ 
																									?klForms a kds:FormClass .
																									?klForms ?p ?o .
																									FILTER(isBlank(?o)) .
																									?o kds:propUri ?property . 
																									?o kds:formFieldOrder ?fieldOrder .
																									OPTIONAL {?o kds:classUri ?classUri } .
																									?property rdfs:domain ?domain .
																									BIND(COALESCE(?classUri,?domain) AS ?propClass) .
																									FILTER((bound(?classUri) && ?domain = ?classUri) || !(bound(?classUri)))
																								}
																								{
																								  ?property ?p1 ?o1 .
																								  OPTIONAL {
																									?o1 ?p2 ?o2 .
																									FILTER(isBlank(?o1))
																									OPTIONAL {
																										?o2 ?p3 ?o3 .
																										FILTER(isBlank(?o2))
																									}
																								  }
																								}
																							}
																						}
																					}
																					# Below binds ensures every variable for each row exists
																					BIND(IF(bound(?p1),?p1,"") AS ?p1_) .
																					BIND(IF(bound(?o1),?o1,"") AS ?o1_) .
																					BIND(IF(bound(?p2),?p2,"") AS ?p2_) .
																					BIND(IF(bound(?o2),?o2,"") AS ?o2_) .
																					BIND(IF(bound(?p3),?p3,"") AS ?p3_) .
																					BIND(IF(bound(?o3),?o3,"") AS ?o3_) .
																				}
																			}
																			BIND(CONCAT('"',REPLACE(REPLACE(REPLACE(REPLACE(STR(?o3_),"\n"," "),"\t"," "),"[ ]+"," "),'"',"&quot"),'"') as ?oV3)
																		}
																		GROUP BY ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ ?p2_ ?o2_ ?p3_
																	}																		
																	BIND(IF(?o3aTest>1,true,false)AS?o3aTestResult) .
																	BIND(IF(?o3a!='""',true,false)AS?o3aShow) .
																	BIND(isBlank(?o2_) AS ?level3Blank) .
																	BIND(IF(?o3aShow&&?level3Blank,CONCAT('"',STR(?p3_),'":',IF(?o3aTestResult,"[",""),?o3a,IF(?o3aTestResult,"]","")),"") as ?pV3)
																}
																GROUP BY ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ ?p2_ ?o2_
															}
															BIND(IF(?p3oTest>1||isblank(?o2_),true,false)AS?p3oTestResult) .
															BIND(IF(?o2_!='',true,false)AS?o2Show) .
															BIND(isBlank(?o2_) AS ?level3pBlank) .
															BIND(IF(?level3pBlank,
																					CONCAT(IF(?p3oTestResult,"{",""),?p3o,IF(?p3oTestResult,"}","")),
																					IF(?o2Show,CONCAT('"',str(?o2_),'"'),"")) AS ?oV2)
														}
														GROUP BY ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ ?p2_ 
													}																		
													BIND(IF(STRSTARTS(?o2a,'"keyCombo":'),CONCAT("{",?o2a,"}"),?o2a) AS ?o2am) .
													BIND(IF(?o2aTest>1,true,false)AS?o2aTestResult) .
													BIND(IF(?o2a!='',true,false)AS?o2aShow) .
													BIND(isBlank(?o1_) AS ?level2Blank) .
													BIND(IF(?o2aShow,CONCAT('"',STR(?p2_),'":',IF(?o2aTestResult,"[",""),?o2am,IF(?o2aTestResult,"]","")),"") as ?pV2)
												}
												GROUP BY ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_ ?o1_ 
											}
											BIND(isBlank(?o1_) as ?trueBlank) .
                                          	BIND(IF(bound(?o1_),(isBlank(?o1_)||(?o1_="_____")),false) AS ?level2pBlank) .
											BIND(IF(?p2oTest>1||(?level2pBlank&&?p2oTest=1),true,false)AS?p2oTestResult) .
											BIND(IF(str(?o1_)!='',true,false)AS?o1Show) .
											BIND((!(bound(?level2pBlank))) as ?level2BoundTest) .
											BIND(IF(?level2BoundTest,
													IF(?o1Show,CONCAT('"',str(?o1_),'"'),""),
													IF(?level2pBlank,
														CONCAT(IF(?p2oTestResult,"{",""),?p2o,IF(?p2oTestResult,"}","")),
														IF(?o1Show,CONCAT('"',str(?o1_),'"'),""))) AS ?oV1)	
										}
										GROUP BY ?klForms ?p ?o ?fieldOrder ?1st  ?propClass  ?p1_
									}																		
									BIND(IF(?o1aTest>1,true,false)AS?o1aTestResult) . 
									BIND(IF(?o1a!='',true,false)AS?o1aShow) .
									BIND((isBlank(?propClass)||?propClass="_____") AS ?level1Blank) .
									BIND(IF(?o1aShow,CONCAT('"',STR(?p1_),'":',IF(?o1aTestResult,"[",""),?o1a,IF(?o1aTestResult,"]","")),"") as ?pV1)
								}
								GROUP BY ?klForms ?p ?o ?fieldOrder ?1st  ?propClass
								ORDER BY ?klForms ?fieldOrder
							}
							BIND(IF(?propClass="_____",true,false)AS?oShow) .
							BIND(IF(?p1oTest>1,true,false)AS?p1oTestResult) .
							BIND(IF(?propClass!="_____",
										CONCAT('{"http://knowledgelinks.io/ns/data-structures/classUri":"',STR(?propClass),'",',?p1o,"}"),
										IF(?oShow,CONCAT(IF(?p1oTestResult,"{",""),?p1o,IF(?p1oTestResult,"}","")),"")) as ?oV)
							BIND(IF(isBlank(?o),?oV,CONCAT('"',str(?o),'"')) AS ?oVf)
						}
						GROUP BY ?klForms ?p 
					}																		
					BIND(IF(?oaTest>1,true,false)AS?oaTestResult) . 
					BIND(CONCAT('"',STR(?p),'":',IF(?oaTestResult,"[",""),?oa,IF(?oaTestResult,"]","")) as ?pV)
				}
				GROUP BY ?klForms 
			}
			BIND(CONCAT('"',STR(?klForms),'":{"http://knowledgelinks.io/ns/data-structures/formUri":"',STR(?klForms),'",',?po,"}") as ?form)
		}
	}
}